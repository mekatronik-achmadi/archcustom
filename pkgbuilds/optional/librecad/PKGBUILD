_repository='LibreCAD'
pkgname=librecad-git
pkgver=2.2.2_alpha.latest.r128.g520ddd07d
pkgrel=1
pkgdesc="A 2D CAD drawing tool based on the community edition of QCad."
arch=('x86_64')
url="https://librecad.org"
license=('GPL')

depends=('muparser' 'qt6-svg' 'qt6-base' 'boost' 'freetype2')
makedepends=('git')
provides=('librecad')
conflicts=('librecad')

source=("git+https://github.com/LibreCAD/$_repository.git")
md5sums=('SKIP')

pkgver()
{
    cd "$_repository"

    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build()
{
    cd "$_repository"

    qmake6 -recursive librecad.pro \
        QMAKE_CPPFLAGS="$CPPFLAGS" \
        QMAKE_CFLAGS="$CFLAGS" \
        QMAKE_CXXFLAGS="$CXXFLAGS" \
        QMAKE_LDFLAGS="$LDFLAGS"

    make
}

package()
{
    cd "$_repository"

    install -m 644 -Dt "$pkgdir/usr/share/pixmaps" librecad/res/main/librecad.png
    install -m 644 -Dt "$pkgdir/usr/share/applications" desktop/librecad.desktop
    install -m 644 -Dt "$pkgdir/usr/share/man/man1" desktop/librecad.1

    cd unix

    install -Dt "$pkgdir/usr/bin" librecad ttf2lff

    install -Dd "$pkgdir/usr/share/librecad"
    cp --recursive -t "$pkgdir/usr/share/librecad" resources/*
}
